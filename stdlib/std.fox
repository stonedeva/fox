const SYS_read 0 end
const SYS_write 1 end
const SYS_open 2 end
const SYS_close 3 end
const SYS_exit 60 end

const stdin 0 end
const stdout 1 end
const stderr 2 end

const sizeof(int32) 4 end
const sizeof(int64) 8 end
const sizeof(ptr) 8 end

const PUTD_BUFFER_CAP 32 end
memory putd_buffer PUTD_BUFFER_CAP end

const O_WRONLY 577 end
const O_RDONLY 0 end
const O_CREAT 438 end


// argv_at - Returns the argument at a given index
// : index => argument
func argv_at int - ptr in
    sizeof(ptr) * argv +
end

// inc64 - Increase 64-bit value at memory address
// : address
func inc64 ptr in
    dup get64 1 + set64
end

// dec64 - Decrease 64-bit value at memory address
// : address
func dec64 ptr in
    dup get64 1 - set64
end

// inc32 - Increase 32-bit value at memory address
// : address
func inc32 ptr in
    dup get32 1 + set32
end

// dec32 - Decrease 32-bit value at memory address
// : address
func dec32 ptr in
    dup get32 1 - set32
end

// inc8 - Increase 8-bit value at memory address
// : address
func inc8 ptr in
    dup get8 1 + set8
end

// dec8 - Decrease 8-bit value at memory address
// : address
func dec8 ptr in
    dup get8 1 - set8
end

// strcmp - Compare two strings
// : n1, s1, n2, s2 => comparison
func strcmp int ptr int ptr - bool in
    take n1 s1 n2 s2 in
	if n1 n2 != do
	    0 return
	end
        0 while dup n1 > do
            if dup s1 + get8 over s2 + get8 != do
		drop
		0 return
            end
            1 +
        end
        drop
    end
    1 return
end

// strchop - Chop whitespaces at beginning from the string
// : pointer => modified pointer
func strchop ptr - ptr in
    peek str in
	0 while dup str + get8 ' ' == do
	    1 +
	end
	str +
    end
end

// cstrlen - Determine length of null-terminated string
// : pointer => length
func cstrlen ptr - int in
    take str in
        0 while dup str + get8 0 != do
            1 +
        end
    end
end

// cstr_to_fstr - Convert a C-style string to a Fox-string
// : pointer => pointer, length
func cstr_to_fstr ptr in
    dup @cstrlen swap
end

// memcpy - Copys a region of memory into another
// : dest, source, size
func memcpy ptr ptr int in
    take dst src n in
	0 while dup n > do
	    dup src + get8
	    over dst + swap set8
	    1 +
	end
	drop
    end
end

// memset - Set a memory region to a specific value
// : source, value, n
func memset ptr int int in
    take src val n in
	0 while dup n > do
	    dup src + val set8
	    1 +
	end
	drop
    end
end

// strcpy - Copy a C-style string into a buffer
// : source, dest
func strcpy ptr ptr in
    take dst src in
	0 while dup src + get8 0 != do
	    dup src + get8
	    over dst + swap set8
	    1 +
	end
	dst + 0 set8
    end
end

// puts - Writes a string to standard output
// : pointer, length
func puts int ptr in
    take len str in
	SYS_write stdout str len syscall
	drop
    end
end

// fputd - Write a number to file
// : number, file descriptor
func fputd int int in
    swap
    0 while over 0 != do
        peek num index in
            putd_buffer PUTD_BUFFER_CAP index - 1 - +
            num 10 % '0' + set8
        end
        1 +
        swap 10 / swap
    end
    swap drop

    take digits fd in
	SYS_write fd 
	putd_buffer PUTD_BUFFER_CAP digits - + digits syscall 
	drop
    end
end

// putd - Write number to standard output
// : number
func putd int in
    0 while over 0 != do
        peek num index in
            putd_buffer PUTD_BUFFER_CAP index - 1 - +
            num 10 % '0' + set8
        end
        1 +
        swap 10 / swap
    end
    drop drop

    SYS_write stdout putd_buffer PUTD_BUFFER_CAP syscall 
    drop
end

// cputs - Write a null-terminated string to standard output
// : pointer
func cputs ptr in
    take str in
	SYS_write stdout str str @cstrlen syscall 
	drop
    end
end

// fopen - Opens a file
// : pointer, mode, permission => file descriptor
func fopen ptr int int - int in
    take path mode perm in
	SYS_open path mode perm syscall
    end
end

// fclose - Closes a file
// : file descriptor
func fclose int in
    take fd in
	SYS_close fd 0 0 syscall
	drop
    end
end

// fputs - Write string to file
// : pointer, length, file descriptor
func fputs int ptr int in
    take len str fd in
	SYS_write fd str len 1 - syscall
	drop
    end
end

// eputs - Write string to standard error
// : pointer, length
func eputs int ptr in
    take len str in
	SYS_write stderr str len syscall
	drop
    end
end

// read - Read string from standard input
// : dest, length
func read ptr int in
    take dst len in
	SYS_read stdin dst len syscall
	drop
    end
end

// fread - Read string from file
// : file descriptor, dest, length
func fread int ptr int in
    take fd dst len in
	SYS_read fd dst len syscall
	drop
    end
end

// exit - Exit program
// : code
func exit int in
    take code in
	SYS_exit code 0 0 syscall
	drop
    end
end

